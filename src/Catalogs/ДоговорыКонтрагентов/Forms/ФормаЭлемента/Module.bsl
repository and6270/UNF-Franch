#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ФФ_ВидДоговораПриИзмененииПосле(Элемент)
	ФФ_УстановитьВидимостьДопЭлементов();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ФФ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	//Левая колонка
	НовыйЭлемент = Элементы.Добавить("ФФ_ЛеваяКолонка", Тип("ГруппаФормы"), Элементы.ЛеваяКолонка);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;

	НовыйЭлемент = Элементы.Добавить("ФФ_Тариф", Тип("Полеформы"), Элементы.ФФ_ЛеваяКолонка);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_Тариф";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	СтруктураПараметровВыбора = Новый СвязьПараметраВыбора("Отбор.Категория", "Объект.КатегорияДоговора",
		РежимИзмененияСвязанногоЗначения.Очищать);
	НовМассив = Новый Массив;
	НовМассив.Добавить(СтруктураПараметровВыбора);
	ФиксМассив = Новый ФиксированныйМассив(НовМассив);
	НовыйЭлемент.СвязиПараметровВыбора = ФиксМассив;

	НовыйЭлемент = Элементы.Добавить("ФФ_ПакетЧасов", Тип("Полеформы"), Элементы.ФФ_ЛеваяКолонка);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_ПакетЧасов";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;

	НовыйЭлемент = Элементы.Добавить("ФФ_ОплатаСрок", Тип("ГруппаФормы"), Элементы.ФФ_ЛеваяКолонка);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	НовыйЭлемент = Элементы.Добавить("ФФ_ВидОплаты", Тип("Полеформы"), Элементы.ФФ_ОплатаСрок);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_ВидОплаты";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
	НовыйЭлемент.МаксимальнаяШирина = 10;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ФФ_РасчитатьСтоимостьЗаМесяц");

	НовыйЭлемент = Элементы.Добавить("ФФ_Срок", Тип("Полеформы"), Элементы.ФФ_ОплатаСрок);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_Срок";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.РежимВыбораИзСписка = Истина;
	Для а = 1 По 12 Цикл
		НовЗначение = НовыйЭлемент.СписокВыбора.Добавить();
		НовЗначение.Значение = а;
		НовЗначение.Представление = "" + а;
	КонецЦикла;
	НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
	НовыйЭлемент.МаксимальнаяШирина = 10;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ФФ_РассчитатьДатуОкончания");

	НовыйЭлемент = Элементы.Добавить("ФФ_ДатаНачалаОкончания", Тип("ГруппаФормы"), Элементы.ФФ_ЛеваяКолонка);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	НовыйЭлемент = Элементы.Добавить("ФФ_ДатаНачала", Тип("Полеформы"), Элементы.ФФ_ДатаНачалаОкончания);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_ДатаНачала";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ФФ_РассчитатьДатуОкончания");

	НовыйЭлемент = Элементы.Добавить("ФФ_ДатаОкончания", Тип("Полеформы"), Элементы.ФФ_ДатаНачалаОкончания);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_ДатаОкончания";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;

	НовыйЭлемент = Элементы.Добавить("ФФ_ГруппаСтоимость", Тип("ГруппаФормы"), Элементы.ФФ_ЛеваяКолонка);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	НовыйЭлемент = Элементы.Добавить("ФФ_Стоимость", Тип("Полеформы"), Элементы.ФФ_ГруппаСтоимость);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_Стоимость";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ФФ_РасчитатьСтоимостьЗаМесяц");

	НовыйЭлемент = Элементы.Добавить("ФФ_СтоимостьМесяц", Тип("ДекорацияФормы"), Элементы.ФФ_ГруппаСтоимость);
	НовыйЭлемент.Заголовок = "";
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	
	//Правая колонка
	НовыйЭлемент = Элементы.Добавить("ФФ_ПраваяКолонка", Тип("ГруппаФормы"), Элементы.ПраваяКолонка);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;

	НовыйЭлемент = Элементы.Добавить("ФФ_РасходнаяНакладная", Тип("Полеформы"), Элементы.ФФ_ПраваяКолонка);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_РасходнаяНакладная";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;

	НовыйЭлемент = Элементы.Добавить("ФФ_ЗаказПокупателя", Тип("Полеформы"), Элементы.ФФ_ПраваяКолонка);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_ЗаказПокупателя";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;

	НовыйЭлемент = Элементы.Добавить("ФФ_ПричинаОтвала", Тип("Полеформы"), Элементы.ФФ_ПраваяКолонка);
	НовыйЭлемент.ПутьКДанным = "Объект.ФФ_ПричинаОтвала";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;

	ФФ_УстановитьВидимостьДопЭлементов();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ФФ_УстановитьВидимостьДопЭлементов()
	Если Объект.ВидДоговора = Перечисления.ВидыДоговоров.СПокупателем Тогда
		Элементы.ФФ_ЛеваяКолонка.Видимость = Истина;
		Элементы.ФФ_ПраваяКолонка.Видимость = Истина;
		Если ДобавитьМесяц(ТекущаяДата(), 2) > Объект.ФФ_ДатаОкончания Тогда
			Элементы.ФФ_ПраваяКолонка.Доступность = Истина;
		Иначе
			Элементы.ФФ_ПраваяКолонка.Доступность = Ложь;
		КонецЕсли;
	Иначе
		Элементы.ФФ_ЛеваяКолонка.Видимость = Ложь;
		Элементы.ФФ_ПраваяКолонка.Видимость =Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФФ_РассчитатьДатуОкончания()
	Если Не ЗначениеЗаполнено(Объект.ФФ_ДатаНачала) Тогда
		Если Не ЗначениеЗаполнено(Объект.ДатаДоговора) Тогда
			Возврат;
		Иначе
			Объект.ФФ_ДатаНачала = Объект.ДатаДоговора;
		КонецЕсли;
	КонецЕсли;
	Объект.ФФ_ДатаОкончания = ДобавитьМесяц(Объект.ФФ_ДатаНачала, Объект.ФФ_Срок);
	Если Объект.ФФ_Срок <> 0 Тогда
		Объект.ФФ_ДатаОкончания = Объект.ФФ_ДатаОкончания - 86400;
	КонецЕсли;
	ФФ_РасчитатьСтоимостьЗаМесяц();
КонецПроцедуры

&НаКлиенте
Процедура ФФ_РасчитатьСтоимостьЗаМесяц()
	Элементы.ФФ_СтоимостьМесяц.Заголовок = "";
	Если Объект.ФФ_Срок <> 0 Тогда
		Если Объект.ФФ_ВидОплаты = ПредопределенноеЗначение("Перечисление.ФФ_ВидыОплатДоговоров.Сразу") Тогда
			Элементы.ФФ_СтоимостьМесяц.Заголовок = "" + Окр(Объект.ФФ_Стоимость / Объект.ФФ_Срок, 2) + " руб./мес.";
		ИначеЕсли Объект.ФФ_ВидОплаты = ПредопределенноеЗначение("Перечисление.ФФ_ВидыОплатДоговоров.Помесячно") Тогда
			Элементы.ФФ_СтоимостьМесяц.Заголовок = "" + Объект.ФФ_Стоимость + " руб./мес.";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти